module InsightClub.Creator.Bot.Api

open Core
open System
open Funogram
open Funogram.Telegram
open Funogram.Telegram.Bot
open Funogram.Telegram.Types
open System.Text.RegularExpressions


// Commands
module Command =
  let start = "/start"
  let help = "/help"
  let new' = "/new"
  let cancel = "/cancel"
  let exit = "/exit"
  let title = "/title"
  let desc = "/desc"
  let show = "/show"
  let edit = "/edit"
  let prev = "/prev"
  let next = "/next"

let getCommands ctx =
  let getInactive () =
    ctx.Update.Message
    |> Option.bind (fun m -> m.Text)
    |> Option.filter ((=) Command.start)
    |> Option.map (always Inactive.Start)

  let getIdle () =
    match ctx.Update.Message with
    | Some { Text = Some (Command Command.help) } ->
      Some Idle.Help

    | Some { Text = Some (Command Command.new') } ->
      Some Idle.CreateCourse

    | Some { Text = Some (Command Command.edit) } ->
      Some (Idle.EditCourse 5)

    | _ ->
      None

  let getCreatingCourse () =
    match ctx.Update with
    | { CallbackQuery = Some { Data = Some (Command Command.cancel) } } ->
      Some CreatingCourse.Cancel

    | { Message = Some { Text = Some (PlainText courseTitle) } } ->
      Some <| CreatingCourse.CreateCourse courseTitle

    | _ ->
      None

  let getEditingCourse () =
    match ctx.Update.CallbackQuery with
    | Some { Data = Some (Command Command.title) } ->
      Some EditingCourse.EditTitle

    | Some { Data = Some (Command Command.desc) } ->
      Some EditingCourse.EditDesc

    | Some { Data = Some (Command Command.exit) } ->
      Some EditingCourse.Exit

    | _ ->
      None

  let getEditingTitle () =
    match ctx.Update with
    | { CallbackQuery = Some { Data = Some (Command Command.cancel) } } ->
      Some EditingTitle.Cancel

    | { Message = Some { Text = Some (PlainText courseTitle) } } ->
      Some <| EditingTitle.SetTitle courseTitle

    | _ ->
      None

  let getEditingDesc () =
    match ctx.Update with
    | { CallbackQuery = Some { Data = Some (Command Command.show) } } ->
      Some EditingDesc.Show

    | { CallbackQuery = Some { Data = Some (Command Command.cancel) } } ->
      Some EditingDesc.Cancel

    | { Message = Some { Text = Some (PlainText courseDesc) } } ->
      Some <| EditingDesc.SetDesc courseDesc

    | _ ->
      None

  let getListingCourses () =
    match ctx.Update.CallbackQuery with
    | Some { Data = Some (CommandParam Command.edit courseId) } ->
      Some (ListingCourses.Select courseId)

    | Some { Data = Some (Command Command.prev) } ->
      Some ListingCourses.Prev

    | Some { Data = Some (Command Command.next) } ->
      Some ListingCourses.Next

    | Some { Data = Some (Command Command.exit) } ->
      Some ListingCourses.Exit

    | _ ->
      None

  { getInactive = getInactive
    getIdle = getIdle
    getCreatingCourse = getCreatingCourse
    getEditingCourse = getEditingCourse
    getEditingTitle = getEditingTitle
    getEditingDesc = getEditingDesc
    getListingCourses = getListingCourses }

// Api helpers
let getUser ctx =
  match ctx.Update with
  | { Message = Some { From = Some user } } ->
    Some user

  | { CallbackQuery = Some { From = user } } ->
    Some user

  | _ ->
    None

let button text command =
  { Text = text
    CallbackData = Some command
    Url = None
    Pay = None
    LoginUrl = None
    CallbackGame = None
    SwitchInlineQuery = None
    SwitchInlineQueryCurrentChat = None }

let inlineMarkup =
  Option.map
    ( fun markup ->
        { InlineKeyboard = List.map Seq.ofList markup } )

let markup =
  inlineMarkup
  >> Option.map InlineKeyboardMarkup

let removeLastMarkupMaybe config lastId userId =
  async
    { if Option.isSome lastId then
        do!
          Api.editMessageReplyMarkupBase
            (Some <| Int userId) lastId None None
          |> Api.api config
          |> Async.Ignore }

let sendMessage config lastId userId text keyboard  =
  if not <| String.IsNullOrEmpty text then
    Api.sendMessageBase
      (Int userId) text None None None None (markup keyboard)
    |> Api.api config
    |> Async.map
      ( fun r ->
          match r with
          | Ok m ->
            m.ReplyMarkup
            |> Option.map (always m.MessageId)

          | Error _ -> None )
  else
    Async.singleton lastId

let answerCallbackQuery config (query: CallbackQuery) text =
  Api.answerCallbackQueryBase
    (Some query.Id) text None None None
  |> Api.api config
  |> Async.Ignore

let editMessage config lastId userId text keyboard =
  let id = Some <| Int userId

  if not <| String.IsNullOrEmpty text then
    Api.editMessageTextBase
      id lastId None text None None (inlineMarkup keyboard)
    |> Api.api config
    |> Async.Ignore
  else
    Async.doNothing

// Text utils
let c s = Regex("\n[ ]+").Replace(s, "\n")
let random = Random()
let randomEmoji () =
  let emojis =
    [| "ü§∑‚Äç‚ôÇÔ∏è"; "üòµ‚Äçüí´"; "üôÑ"; "ü§™"; "üôÄ"
       "üò≠"; "üò£"; "ü•∫"; "üòë"; "üí©" |]

  emojis.[ random.Next(emojis.Length) ]

// Messages
let idleMsg (user: User) =
  function
  | Idle.Started ->
    let lastName =
      user.LastName
      |> Option.map ((+) " ")
      |> Option.defaultValue ""

    c$"–î–æ–±—Ä–æ –ø–æ–∂–∞–ª–æ–≤–∞—Ç—å –≤ InsightClub.Creator.Bot, {user.FirstName} \
      {lastName}! ‚ú® –° –ø–æ–º–æ—â—å—é —ç—Ç–æ–≥–æ –±–æ—Ç–∞ –í—ã –º–æ–∂–µ—Ç–µ –∫–æ–Ω—Å—Ç—Ä—É–∏—Ä–æ–≤–∞—Ç—å —Å–≤–æ–∏ \
      –∫—É—Ä—Å—ã! üòé

      –û—Ç–ø—Ä–∞–≤—å—Ç–µ /help –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –ø–æ–º–æ—â–∏ üëÄ"

  | Idle.Helping ->
    c$"–î–æ–±—Ä–æ –ø–æ–∂–∞–ª–æ–≤–∞—Ç—å –≤ —Å–ø—Ä–∞–≤–∫—É InsightClub.Creator.Bot! ü§ñ

      –≠—Ç–æ—Ç –±–æ—Ç –∏–º–µ–µ—Ç –Ω–µ—Å–∫–æ–ª—å–∫–æ —Ä–µ–∂–∏–º–æ–≤ üßû‚Äç‚ôÇÔ∏è –ù–∞ –¥–∞–Ω–Ω—ã–π –º–æ–º–µ–Ω—Ç –æ–Ω –Ω–∞—Ö–æ–¥–∏—Ç—Å—è \
      –≤ —Ä–µ–∂–∏–º–µ –æ–∂–∏–¥–∞–Ω–∏—è. –í—Å–µ –æ—Å—Ç–∞–ª—å–Ω—ã–µ —Ä–µ–∂–∏–º—ã –∏–º–µ—é—Ç –≤—Å–ø–æ–º–æ–≥–∞—Ç–µ–ª—å–Ω—ã–µ \
      –∫–ª–∞–≤–∏–∞—Ç—É—Ä—ã, –∫–æ—Ç–æ—Ä—ã–µ –ø–æ–º–æ–≥—É—Ç –í–∞–º –ª–µ–≥–∫–æ —Ä–∞–∑–æ–±—Ä–∞—Ç—å—Å—è –≤ —Ñ—É–Ω–∫—Ü–∏–æ–Ω–∞–ª–µ.

      {Command.new'} - –°–æ–∑–¥–∞—Ç—å –Ω–æ–≤—ã–π –∫—É—Ä—Å ‚ö°Ô∏è
      {Command.edit} - –†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å —Å—É—â–µ—Å—Ç–≤—É—é—â–∏–π –∫—É—Ä—Å üìù
      {Command.help} - –ü–æ–ª—É—á–∏—Ç—å –ø–æ–º–æ—â—å (–í—ã —Å–µ–π—á–∞—Å –∑–¥–µ—Å—å) üëÄ

      –£—á–∏—Ç—ã–≤–∞–π—Ç–µ, —á—Ç–æ –∫–æ–º–∞–Ω–¥–∞ {Command.help} —Ä–∞–±–æ—Ç–∞–µ—Ç —Ç–æ–ª—å–∫–æ –≤ —Ä–µ–∂–∏–º–µ –æ–∂–∏–¥–∞–Ω–∏—è. \
      –í –æ—Å—Ç–∞–ª—å–Ω—ã—Ö —Ä–µ–∂–∏–º–∞—Ö –æ–Ω–∞ –Ω–µ —Ä–∞—Å–ø–æ–∑–Ω–∞—ë—Ç—Å—è, –∏–±–æ –∏—Ö –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å –ø–æ–º–æ–∂–µ—Ç \
      –í–∞–º –ª–µ–≥–∫–æ —Ä–∞–∑–æ–±—Ä–∞—Ç—å—Å—è üî•"

  | Idle.NoCourses ->
    c$"–£ –í–∞—Å –ø–æ–∫–∞ –Ω–µ—Ç –∫—É—Ä—Å–æ–≤ {randomEmoji ()}
      –°–æ–∑–¥–∞–π—Ç–µ –Ω–æ–≤—ã–π, –æ—Ç–ø—Ä–∞–≤–∏–≤ –∫–æ–º–∞–Ω–¥—É {Command.new'} ü§π‚Äç‚ôÇÔ∏è"

  | Idle.CreateCanceled ->
    "–°–æ–∑–¥–∞–Ω–∏–µ –∫—É—Ä—Å–∞ –æ—Ç–º–µ–Ω–µ–Ω–æ üëå"

  | Idle.EditCanceled ->
    "–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –æ—Ç–º–µ–Ω–µ–Ω–æ üëå"

  | Idle.ExitedEditing ->
    "–ö–∞–∫ –ø–æ–∂–µ–ª–∞–µ—Ç–µ üßû‚Äç‚ôÇÔ∏è –†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –∑–∞–≤–µ—Ä—à–µ–Ω–æ."

  | Idle.Error ->
    c$"–ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞ {randomEmoji ()}
      –û—Ç–ø—Ä–∞–≤—å—Ç–µ {Command.help} –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –ø–æ–º–æ—â–∏ üëÄ"

let creatingCourseMsg =
  function
  | CreatingCourse.Started ->
    c "–†–µ–∂–∏–º —Å–æ–∑–¥–∞–Ω–∏—è –Ω–æ–≤–æ–≥–æ –∫—É—Ä—Å–∞ üßö‚Äç‚ôÇÔ∏è
      –ö–∞–∫ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã –Ω–∞–∑–≤–∞—Ç—å –Ω–æ–≤—ã–π –∫—É—Ä—Å? üìù"

  | CreatingCourse.TitleReserved ->
    c "–ö—É—Ä—Å —Å —Ç–∞–∫–∏–º –Ω–∞–∑–≤–∞–Ω–∏–µ–º —É–∂–µ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç. ü§∑‚Äç‚ôÇÔ∏è
      –ü–æ–∂–∞–ª—É–π—Å—Ç–∞, –≤—ã–±–µ—Ä–∏—Ç–µ –¥—Ä—É–≥–æ–µ."

  | CreatingCourse.Error ->
    c$"–ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞ {randomEmoji ()}

      –†–µ–∂–∏–º —Å–æ–∑–¥–∞–Ω–∏—è –Ω–æ–≤–æ–≥–æ –∫—É—Ä—Å–∞.
      –ö–∞–∫ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã –Ω–∞–∑–≤–∞—Ç—å –Ω–æ–≤—ã–π –∫—É—Ä—Å? üìù"

let editingCourseMsg =
  function
  | EditingCourse.CourseCreated ->
    c "–ö—É—Ä—Å —Å–æ–∑–¥–∞–Ω! ‚úÖ

      –†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –î–ª—è –ø—Ä–æ—Å–º–æ—Ç—Ä–∞ –¥–∞–Ω–Ω—ã—Ö –æ –∫—É—Ä—Å–µ –≤—ã–±–µ—Ä–∏—Ç–µ —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–∏–π —Ä–∞–∑–¥–µ–ª. \
      –ü–æ–º–∏–º–æ –ø—Ä–æ—Å–º–æ—Ç—Ä–∞, —Ä–∞–∑–¥–µ–ª—ã –ø–æ–∑–≤–æ–ª—è—é—Ç —Ç–∞–∫–∂–µ –∏ \
      —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–∏–µ –¥–∞–Ω–Ω—ã–µ –∫—É—Ä—Å–∞."

  | EditingCourse.Editing ->
    c "–†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –î–ª—è –ø—Ä–æ—Å–º–æ—Ç—Ä–∞ –¥–∞–Ω–Ω—ã—Ö –æ –∫—É—Ä—Å–µ –≤—ã–±–µ—Ä–∏—Ç–µ —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–∏–π —Ä–∞–∑–¥–µ–ª. \
      –ü–æ–º–∏–º–æ –ø—Ä–æ—Å–º–æ—Ç—Ä–∞, —Ä–∞–∑–¥–µ–ª—ã –ø–æ–∑–≤–æ–ª—è—é—Ç —Ç–∞–∫–∂–µ –∏ \
      —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–∏–µ –¥–∞–Ω–Ω—ã–µ –∫—É—Ä—Å–∞."

  | EditingCourse.TitleCanceled ->
    c "–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –Ω–∞–∑–≤–∞–Ω–∏—è –æ—Ç–º–µ–Ω–µ–Ω–æ üëå

      –†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –í—ã–±–µ—Ä–∏—Ç–µ —á—Ç–æ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã —Å–¥–µ–ª–∞—Ç—å –¥–∞–ª—å—à–µ."

  | EditingCourse.TitleSet ->
    c "–ù–∞–∑–≤–∞–Ω–∏–µ –∫—É—Ä—Å–∞ –æ–±–Ω–æ–≤–ª–µ–Ω–æ! ‚úÖ

      –†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –í—ã–±–µ—Ä–∏—Ç–µ —á—Ç–æ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã —Å–¥–µ–ª–∞—Ç—å –¥–∞–ª—å—à–µ."

  | EditingCourse.DescCanceled ->
    c "–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –æ–ø–∏—Å–∞–Ω–∏—è –æ—Ç–º–µ–Ω–µ–Ω–æ üëå

      –†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –í—ã–±–µ—Ä–∏—Ç–µ —á—Ç–æ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã —Å–¥–µ–ª–∞—Ç—å –¥–∞–ª—å—à–µ."

  | EditingCourse.DescSet ->
    c "–û–ø–∏—Å–∞–Ω–∏–µ –∫—É—Ä—Å–∞ –æ–±–Ω–æ–≤–ª–µ–Ω–æ! ‚úÖ

      –†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –í—ã–±–µ—Ä–∏—Ç–µ —á—Ç–æ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã —Å–¥–µ–ª–∞—Ç—å –¥–∞–ª—å—à–µ."

  | EditingCourse.Error ->
    c$"–ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞ {randomEmoji ()}

      –†–µ–∂–∏–º —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞ ‚úèÔ∏è
      –í—ã–±–µ—Ä–∏—Ç–µ —á—Ç–æ –í—ã —Ö–æ—Ç–µ–ª–∏ –±—ã —Å–¥–µ–ª–∞—Ç—å –¥–∞–ª—å—à–µ."

let editingTitleMsg title =
  function
  | EditingTitle.Started ->
    c$"–†–µ–¥–∞–∫—Ç–∏—Ä—É–µ–º –Ω–∞–∑–≤–∞–Ω–∏–µ –∫—É—Ä—Å–∞ ü•∏

      –¢–µ–∫—É—â–µ–µ –Ω–∞–∑–≤–∞–Ω–∏–µ –∫—É—Ä—Å–∞: {title}
      –û—Ç–ø—Ä–∞–≤—å—Ç–µ –Ω–æ–≤–æ–µ, —á—Ç–æ–± –∏–∑–º–µ–Ω–∏—Ç—å."

  | EditingTitle.TitleReserved ->
    c$"–ö—É—Ä—Å —Å —Ç–∞–∫–∏–º –Ω–∞–∑–≤–∞–Ω–∏–µ–º —É–∂–µ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç ü§∑‚Äç‚ôÇÔ∏è

      –¢–µ–∫—É—â–µ–µ –Ω–∞–∑–≤–∞–Ω–∏–µ –∫—É—Ä—Å–∞: {title}
      –û—Ç–ø—Ä–∞–≤—å—Ç–µ –Ω–æ–≤–æ–µ, —á—Ç–æ–± –∏–∑–º–µ–Ω–∏—Ç—å."

  | EditingTitle.Error ->
    c$"–ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞ {randomEmoji ()}

      –¢–µ–∫—É—â–µ–µ –Ω–∞–∑–≤–∞–Ω–∏–µ –∫—É—Ä—Å–∞: {title}
      –û—Ç–ø—Ä–∞–≤—å—Ç–µ –Ω–æ–≤–æ–µ, —á—Ç–æ–± –∏–∑–º–µ–Ω–∏—Ç—å."

let editingDescMsg =
  function
  | EditingDesc.Started ->
    c "–†–µ–¥–∞–∫—Ç–∏—Ä—É–µ–º –æ–ø–∏—Å–∞–Ω–∏–µ –∫—É—Ä—Å–∞ üëΩ

      –û—Ç–ø—Ä–∞–≤—å—Ç–µ —Ç–µ–∫—Å—Ç, —á—Ç–æ–± –∏–∑–º–µ–Ω–∏—Ç—å –æ–ø–∏—Å–∞–Ω–∏–µ.
      –ü–æ—Å—Ç–∞—Ä–∞–π—Ç–µ—Å—å —Å–¥–µ–ª–∞—Ç—å –µ–≥–æ –ø–æ–Ω—è—Ç–Ω—ã–º –∏ —á–∏—Ç–∞–µ–º—ã–º. –≠—Ç–æ —Ç–æ, —á—Ç–æ –í–∞—à–∏ –∫–ª–∏–µ–Ω—Ç—ã \
      –±—É–¥—É—Ç –≤–∏–¥–µ—Ç—å –≤ –ø–µ—Ä–≤—É—é –æ—á–µ—Ä–µ–¥—å, –Ω–µ —Å—á–∏—Ç–∞—è –Ω–∞–∑–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞."

  | EditingDesc.Error ->
    c$"–ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞ {randomEmoji ()}

      –û—Ç–ø—Ä–∞–≤—å—Ç–µ —Ç–µ–∫—Å—Ç, —á—Ç–æ–± –∏–∑–º–µ–Ω–∏—Ç—å –æ–ø–∏—Å–∞–Ω–∏–µ –∫—É—Ä—Å–∞.
      –ü–æ—Å—Ç–∞—Ä–∞–π—Ç–µ—Å—å —Å–¥–µ–ª–∞—Ç—å –µ–≥–æ –ø–æ–Ω—è—Ç–Ω—ã–º –∏ —á–∏—Ç–∞–µ–º—ã–º. –≠—Ç–æ —Ç–æ, —á—Ç–æ –í–∞—à–∏ –∫–ª–∏–µ–Ω—Ç—ã \
      –±—É–¥—É—Ç –≤–∏–¥–µ—Ç—å –≤ –ø–µ—Ä–≤—É—é –æ—á–µ—Ä–µ–¥—å, –Ω–µ —Å—á–∏—Ç–∞—è –Ω–∞–∑–≤–∞–Ω–∏—è –∫—É—Ä—Å–∞."

let listingCoursesMsg page count courseCount msg =
  let m s =
    match msg with
    | ListingCourses.Started ->
      s

    | ListingCourses.Error ->
      c$"–ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞. {randomEmoji ()}

        {s}"

  let min = page * count + 1
  let max = page * count + courseCount

  if min = max
  then $"–ö—É—Ä—Å ‚Ññ {min}"
  else $"–ö—É—Ä—Å—ã —Å ‚Ññ {min} –ø–æ ‚Ññ {max}"
  |> m
  |> c

module Button =
  let cancel = "–û—Ç–º–µ–Ω–∞ ‚ùå"
  let exit = "–í—ã—Ö–æ–¥ üö™"
  let title = "–ù–∞–∑–≤–∞–Ω–∏–µ ‚úèÔ∏è"
  let desc = "–û–ø–∏—Å–∞–Ω–∏–µ üñã"
  let show = "–ü–æ–∫–∞–∑–∞—Ç—å üëÅ"
  let prev = "–ù–∞–∑–∞–¥ ‚¨ÖÔ∏è"
  let next = "–í–ø–µ—Ä—ë–¥ ‚û°Ô∏è"

// Response for state
let handleState (ctx: UpdateContext) connection creatorId lastId state = async {
  // onUpdate must ensure user is available, so this call is safe
  let user = Option.get <| getUser ctx

  let! message, keyboard = async {
    match state with
    | Inactive ->
      return String.Empty, None

    | Idle msg ->
      return idleMsg user msg, None

    | CreatingCourse msg ->
      return
        creatingCourseMsg msg,
        Some [ [ button Button.cancel Command.cancel ] ]

    | EditingCourse (_, msg) ->
      return
        editingCourseMsg msg,
        Some
          [ [ button Button.title Command.title
              button Button.desc Command.desc ]
            [ button Button.exit Command.exit ] ]

    | EditingTitle (_, title, msg) ->
      return
        editingTitleMsg title msg,
        Some [ [ button Button.cancel Command.cancel ] ]

    | EditingDesc (_, msg) ->
      return
        editingDescMsg msg,
        Some [ [ button Button.show Command.show
                 button Button.cancel Command.cancel ] ]

    | ListingCourses (page, count, msg) ->
      let! courses = Repo.getCourses connection creatorId page count

      return
        listingCoursesMsg page count (List.length courses) msg,
        ( courses
          |> List.map
            ( fun (id, title) ->
                [ button title $"{Command.edit} {id}" ] ) )
        @ [ [ button Button.prev Command.prev
              button Button.next Command.next ]
            [ button Button.exit Command.exit ] ]
        |> Some }

  match ctx.Update with
  | { Message = Some _ } ->
    let! _ =
      removeLastMarkupMaybe ctx.Config lastId user.Id
      |> Async.StartChild

    return!
      sendMessage ctx.Config lastId user.Id message keyboard

  | { CallbackQuery = Some query } ->
    let! _ =
      answerCallbackQuery ctx.Config query None
      |> Async.StartChild

    if lastId.IsSome then
      let! _ =
        editMessage ctx.Config lastId user.Id message keyboard

      return
        keyboard
        |> Option.bind (always lastId)
    else
      return!
        sendMessage ctx.Config lastId user.Id message keyboard

  | _ ->
    return lastId }

// Response for effect
let handleEffect (ctx: UpdateContext) lastId =
  // onUpdate must ensure user is available, so this call is safe
  let user = Option.get <| getUser ctx
  let config = ctx.Config

  function
  | Nothing ->
    Async.singleton lastId

  | ShowDesc text -> async {
    let! _ =
      removeLastMarkupMaybe config lastId user.Id
      |> Async.StartChild

    let text =
      if String.IsNullOrEmpty text
      then $"–£ –í–∞—à–µ–≥–æ –∫—É—Ä—Å–∞ –ø–æ–∫–∞ –Ω–µ—Ç –æ–ø–∏—Å–∞–Ω–∏—è {randomEmoji ()}"
      else text

    do!
      Api.sendMessage user.Id text
      |> Api.api config
      |> Async.Ignore

    return None }

  | InformNoPrev -> async {
    match ctx.Update.CallbackQuery with
    | Some query ->
      let! _ =
        answerCallbackQuery ctx.Config query (Some "–í—ã –¥–æ—à–ª–∏ –¥–æ –º–∏–Ω–∏–º—É–º–∞")
        |> Async.StartChild

      return lastId

    | None ->
      return lastId }

  | InformNoNext -> async {
    match ctx.Update.CallbackQuery with
    | Some query ->
      let! _ =
        answerCallbackQuery ctx.Config query (Some "–í—ã –¥–æ—à–ª–∏ –¥–æ –º–∞–∫—Å–∏–º—É–º–∞")
        |> Async.StartChild

      return lastId

    | None ->
      return lastId }

// Main function
let onUpdate getConnection ctx =
  let update (user: User) = async {
    use connection = getConnection ()
    let! creatorId, lastId, state = State.get connection user.Id
    let services = Services.get connection creatorId
    let commands = getCommands ctx
    let! (state, effect) = update services commands state
    let! lastId = handleEffect ctx lastId effect
    let! lastId = handleState ctx connection creatorId lastId state
    do! State.update connection creatorId lastId state }

  ctx
  |> getUser // Ensure user is available
  |> Option.map update
  |> Option.defaultValue Async.doNothing
